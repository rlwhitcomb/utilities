#
# Testing the distribution of random numbers
#
$quiet on
$quotestrings off

define bucket(v) = {
  case v of {
    < .1       : 0,
    >=< .1, .2 : 1,
    >=< .2, .3 : 2,
    >=< .3, .4 : 3,
    >=< .4, .5 : 4,
    >=< .5, .6 : 5,
    >=< .6, .7 : 6,
    >=< .7, .8 : 7,
    >=< .8, .9 : 8,
       default : 9
  }
}

const NUMBER_OF_VALUES = 10000

v = []
loop over NUMBER_OF_VALUES { v += random() }

mean = sumof(v)/length(v)

variance = 0
loop over v { variance += (__ - mean)² }
variance /= length v
stddev = sqrt variance

freq = []
fill(freq, 0, 10)
loop over v {
  b = bucket(__)
  freq[b]++
}
$assert sumof(freq) == length(v)

chisq = 0
const expected = length(v) / length(freq)
loop f over freq {
  chisq += ((f - expected)²) / expected
}

b = 0
$quiet pop
$resultsonly on {

print '"Random" Function Test Results'
print '------------------------------'

`# Values = $NUMBER_OF_VALUES`

`Minimum  = ${min v}`
`Maximum  = ${max v}`
`Average  = $mean`
`Std Dev  = $stddev`
`\u03C7\u00B2       = $chisq`

print
print 'Bucket distribution'
print '-------------------'
loop f over freq {
  print `${b++}. $f`
}
}
