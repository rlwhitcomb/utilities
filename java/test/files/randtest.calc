#
# Testing the distribution of random numbers
#
$quiet on

define bucket(v) = {
  case v of {
    < .1         : 0,
    < .2 && >= .1: 1,
    < .3 && >= .2: 2,
    < .4 && >= .3: 3,
    < .5 && >= .4: 4,
    < .6 && >= .5: 5,
    < .7 && >= .6: 6,
    < .8 && >= .7: 7,
    < .9 && >= .8: 8,
          default: 9
  }
}

v = []
loop over 10000 { v += random() }

mean = sumof(v)/length(v)

variance = 0
loop over v { variance += (__ - mean)**2 }
variance /= length v
stddev = sqrt variance

freq = []
fill(freq, 0, 10)
loop over v {
  b = bucket(__)
  freq[b]++
}
$assert sumof(freq) == length(v)

chisq = 0
const expected = length(v) / length(freq)
loop f over freq {
  chisq += ((f - expected) ** 2) / expected
}

b = 0
$quiet pop
$resultsonly on {

`Minimum = ${min v}` @q
`Maximum = ${max v}` @q
`Average = $mean`    @q
`Std Dev = $stddev`  @q
`\u03C7\u00B2      = $chisq` @q

print 'Bucket distribution'
print '-------------------'
loop f over freq {
  print `${b++}. $f`
}
}
