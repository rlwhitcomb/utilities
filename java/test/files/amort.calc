//
// Print an amortization schedule for a loan
//   $0 = principal amount
//   $1 = yearly interest rate (percentage)
//   $2 = number of years
//
$assert defined(PMT), 'The "library.calc" file must be included.'
$quiet on {
$separators on

if isnull($0) {
    display 'Usage: c amort -- PRINCIPAL RATE YEARS'
    leave 1
}

payment = PMT(P=$0, R=$1, N=$2)

principal = $0
monthly_interest = $1 / 100 / 12
months = $2 * 12

total_payments = 0
total_interest = 0

display `Monthly payment on $$$principal at ${$1}% interest for ${$2} years is $$$payment.`
display

loop m over months {
    var interest = round(monthly_interest * principal, 2)

    if principal <= payment {
        payment = principal + interest
    }
    var principal_decrease = payment - interest

    total_payments += payment
    total_interest += interest

    display `Month: $m Balance: ${principal@2,$}, Payment: ${payment@2,$}, Interest: ${interest@2,$}, Principal: ${principal_decrease@2,$}`
    principal -= principal_decrease
}

display
display `Total of $months monthly payments is ${total_payments@2,$}, Total interest is ${total_interest@2,$}`

}

