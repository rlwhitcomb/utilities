/*
 * Problem 47 from Project Euler (projecteuler.net)
 *
 * Distinct Primes Factors
 *
 * The first two consecutive numbers to have two distinct prime factors are:
 *
 *                                   14 = 2 × 7
 *                                   15 = 3 × 5
 *
 * The first three consecutive numbers to have three distinct prime factors are:
 *
 *                                   644 = 2² × 7 × 23
 *                                   645 = 3 × 5 × 43
 *                                   646 = 2 × 17 × 19
 *
 * Find the first four consecutive integers to have four distinct prime factors each. What is the first of these numbers?
 */
$requires 2.40.20 // for the "not" keyword
$quiet on {

/*
 * Get the set of unique prime factors for the given number.
 *
 * @param n Number to test.
 * @return  Array of unique prime factors for that number.
 */
define pfacts(n) = unique(pfactors(n))

/* Number of consecutive numbers to have this many distinct factors. */
var goal = 2
/* The final goal value to achieve. */
const FINAL_GOAL = 4

/* The current number value we're testing. */
var value = 9

/* End of calculation flag; once the final goal has been reached. */
var end = false

while not end {
   const facts = pfacts(++value)
   if length(facts) == goal {
      var val = value + 1

inner:loop over goal - 1 {
         if length(pfacts(val)) != goal leave inner:
         val++
      }

      if val == value + goal {
         print 'The first', goal@w, 'consecutive numbers to have', goal@w, `distinct prime factors is $value.`
         end = goal++ == FINAL_GOAL
      }
   }
}

} // end of quiet on

