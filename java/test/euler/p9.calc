/*
 * Problem 9 from Project Euler (projecteuler.net)
 *
 * Special Pythagorean triplet
 *
 * A Pythagorean triplet is a set of three natural numbers, a < b < c, for which,
 *
 *     a**2 + b**2 = c**2
 *
 * For example, 3**2 + 4**2 = 9 + 16 = 25 = 5**2.
 *
 * There exists exactly one Pythagorean triplet for which a + b + c = 1000.
 * Find the product abc.
 */
$quiet on {

const SUM = 1000
var triplet = []

/*
 * Return a list of the a, b, c values if there is a solution, or an empty list
 * if no solution is found.
 */
define inner(a) = {
   var result = [ ]
   const a2 = a*a
   loop b over a+1 ... SUM-1 {
      const c = SUM - a - b
      if a2 + b*b == c*c {
         result = [ a, b, c ]
         leave
      }
   }
   result
}

loop a over 1 ... SUM-1 {
   const result = inner(a)
   if length(result) {
      triplet = result
      leave
   }
}

}

$resultsonly on {

`The Pythagorean triplet whose sum is $SUM $triplet has a product of ${productof triplet}.`

}
