/*
 * Problem 9 from Project Euler (projecteuler.net)
 *
 * Special Pythagorean triplet
 *
 * A Pythagorean triplet is a set of three natural numbers, a < b < c, for which,
 *
 *     a² + b² = c²
 *
 * For example, 3² + 4² = 9 + 16 = 25 = 5².
 *
 * There exists exactly one Pythagorean triplet for which a + b + c = 1000.
 * Find the product abc.
 */
$quiet on {

const SUM = 1000

/*
 * Return a list of the a, b, c values if there is a solution, or an empty list
 * if no solution is found.
 */
define solve() = {
   loop a over SUM-1 {
      const a2 = a²
      loop b over a+1 ... SUM-1 {
         const c = SUM - a - b
         if a2 + b² == c² leave solve: [ a, b, c ]
      }
   }
   [ ]
}

const triplet = solve()

}

$resultsonly on {

`The Pythagorean triplet whose sum is $SUM $triplet has a product of ${productof triplet}.`

}
