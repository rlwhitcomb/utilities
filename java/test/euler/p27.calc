/*
 * Problem 27 from Project Euler (projecteuler.net)
 *
 * Quadratic Primes
 *
 * Euler discovered the remarkable quadratic formula:
 *
 *      ğ‘›Â² + ğ‘› + 41
 *
 * It turns out that the formula will produce 40 primes for the consecutive integer values
 * 0 â‰¤ ğ‘› â‰¤ 39. However, when ğ‘› = 40, 40Â² + 40 + 41 = 40(40 + 1) + 41 is divisible by 41, and
 * certainly when ğ‘› = 41, 41Â² + 41 + 41 is clearly divisible by 41.
 *
 * The incredible formula ğ‘›Â² âˆ’ 79ğ‘› + 1601 was discovered, which produces 80 primes for the consecutive values
 * 0 â‰¤ ğ‘› â‰¤ 79. The product of the coefficients, âˆ’79 and 1601, is âˆ’126479.
 *
 * Considering quadratics of the form:
 *
 *      ğ‘›Â² + ğ‘ğ‘› + ğ‘, where |ğ‘| < 1000 and |ğ‘| â‰¤ 1000
 *
 *      where |ğ‘›| is the modulus/absolute value of ğ‘›
 *      e.g.  |11| = 11 and |âˆ’4| = 4
 *
 * Find the product of the coefficients, ğ‘ and ğ‘, for the quadratic expression that produces the maximum
 * number of primes for consecutive values of ğ‘›, starting with ğ‘› = 0.
 */
$quiet on {

$unlimited

const A_MAX = 1000-1
const B_MAX = 1000

/*
 * Return a list of the prime numbers smaller than the given number.
 *
 * @param n Limit for the list of primes.
 * @return  Array of prime numbers smaller than the input.
 */
define primes(n) = {
   var list = []
   if n > 2 list += 2
   loop x over 3..n,2 {
      if isprime(x) list += x
   }
   list
}

define quadratic(n, a, b) = nÂ² + a*n + b

var max_a = 0, max_b = 0
var max_number_primes = 0

/*
 * Since "quadratic(n, a, b)" must be prime when n == 0, then clearly b must be prime always,
 * so restrict "b" to the list of primes < B_MAX.
 */
const B_VALUES = primes(B_MAX)

loop a over -A_MAX..A_MAX {
   loop b over B_VALUES {
      // And since "b" will always be prime here, we can start n at 1 instead of 0
      var n = 1
      while isprime(quadratic(n, a, b)) n++
      if n > max_number_primes {
         max_number_primes = n
         max_a = a
         max_b = b
      }
   }
}

}

$resultsonly on {

`Product of the coefficients of the quadratic that produces the most primes is ${max_a * max_b}, number of primes is $max_number_primes, and a = $max_a, b = $max_b.`

}
