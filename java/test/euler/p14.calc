/*
 * Problem 14 from Project Euler (projecteuler.net)
 *
 * Longest Collatz sequence
 *
 * The following iterative sequence is defined for the set of positive integers:
 *
 *   n → n/2 (n is even)
 *   n → 3n + 1 (n is odd)
 *
 * Using the rule above and starting with 13, we generate the following sequence:
 *
 *   13 → 40 → 20 → 10 → 5 → 16 → 8 → 4 → 2 → 1
 *
 * It can be seen that this sequence (starting at 13 and finishing at 1) contains 10 terms.
 * Although it has not been proved yet (Collatz Problem), it is thought that all starting numbers finish at 1.
 *
 * Which starting number, under one million, produces the longest chain?
 *
 * NOTE: Once the chain starts the terms are allowed to go above one million.
 */
$quiet on {

const START_LIMIT = 1e6
var savedLengths = [ 0, 1 ]
fill(savedLengths, -1, 2, START_LIMIT - 2)

define collatzLength(start) = {
   var seqlen = 0
   var value = start
   while value >= START_LIMIT || savedLengths[value] < 0 {
      seqlen++
      if value % 2 == 0 { value \= 2            }
                   else { value = 3 * value + 1 }
   }
   seqlen += savedLengths[value]
}

var longestStartValue = 0
var longestSequence = 0

loop start over 2..START_LIMIT-1 {
   const seqlen = collatzLength(start)

   // Cache the already calculated values for faster computation next time
   savedLengths[start] = seqlen
   // Once we know a length, then the length at n*2 will be one longer
   if start < START_LIMIT / 2 {
      savedLengths[start * 2] = seqlen + 1
   }

   if seqlen > longestSequence {
      longestSequence = seqlen
      longestStartValue = start
   }
}

}

$resultsonly on {

`The starting number under $START_LIMIT with the longest Collatz sequence is $longestStartValue ($longestSequence values).`

}
